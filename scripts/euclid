#!/usr/bin/awk -f
# visualisation of extended euclides algorithm (ax + by = gcd(a,b))
# usage: euclid a b
# You should pipe output to `marktable` or `column -t`, as it is barely readable otherwise
function euclid(a, b) {
    orig_a = a; orig_b = b; 
    old_x = 1; x = 0;
    old_y = 0; y = 1;
    print "r q x y a*x+b*y=r";
    print a,"-",old_x,old_y,orig_a"*"old_x"+"orig_b"*"old_y"="a;
    while(b != 0) {
        q = int(a/b);
        tmp = x; x = old_x - q * x; old_x = tmp;
        tmp = y; y = old_y - q * y; old_y = tmp;
        tmp = b; b = a % b; a = tmp;
        print a,q,old_x,old_y,orig_a"*"old_x"+"orig_b"*"old_y"="a;
    }
}
BEGIN { 
   if(ARGC == 3) euclid(ARGV[1], ARGV[2]);
   else { print "usage: euclid a b"; exit 1 }
}
